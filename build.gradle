import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'xyz.hlafaille'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    // implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

///////
/**
 * Tailwind Compilation
 */
processResources {
    exclude '**/*/style.css'
    exclude '**/*/*.js'
}

tasks.register('compileTailwind', Exec) {
    String cmd = "npx"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        cmd = "C:\\Program Files\\nodejs\\npx.cmd"
    }

    commandLine cmd, 'tailwindcss',
            // '--content', 'C:/Users/water/IdeaProjects/seizuretracker/src/main/resources/templates',
            '-i', 'src/main/resources/static/style.css',
            '-o', 'build/resources/main/static/style.css'
}

/**
 * JavaScript Bundling
 */

tasks.register('bundleJavaScript', Exec) {
    // add node_modules/.bin to the path
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        environment 'PATH', System.getenv("PATH") + ";" + new File("node_modules/.bin").absolutePath
        println System.getenv("PATH")
    } else {
        environment 'PATH', System.getenv("PATH") + ":" + new File("node_modules/.bin").absolutePath
    }

    // Define the directory you want to list files from
    File dir = new File('src/main/js') // Change to your directory

    // establish our command
    List<String> command = new ArrayList<>()
    command.add("esbuild")

    // Check if the directory exists
    if (dir.exists() && dir.isDirectory()) {
        // Recursively process all files in the directory
        dir.eachFileRecurse { File file ->
            // Print the absolute path of each file
            command.add(file.absolutePath.toString())
        }
    } else {
        println "Directory does not exist: ${dir.absolutePath}"
        System.exit(1)
    }

    command.add("--bundle")
    command.add("--outfile=build/resources/main/static/bundle.js")
    commandLine command
}
// Make the Java build task depend on the Tailwind compile task
processResources.dependsOn compileTailwind